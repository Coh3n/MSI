
var
  SL_Trees: array of TIntegerArray;
  SL_TreeIndexes: TIntegerArray;

(**
 * Loops through all MSI scripts, sorting them into skill categories. Sets
 * SL_Trees array.
 *)
procedure SL_SetTrees();
var
  i: integer;
begin
  setLength(SL_Trees, SKILL_COUNT);
  setLength(SL_TreeIndexes, SKILL_COUNT);

  for i := 0 to high(MSI_Scripts) do
  begin
    if (MSI_Scripts[i].Name = '') then
      continue;

    with MSI_Scripts[i] do
    begin
      setLength(SL_Trees[skillConst], length(SL_Trees[skillConst]) + 1);
      SL_Trees[skillConst][high(SL_Trees[skillConst])] := constant;
    end;
  end;
end;

(**
 * Will either expand or contract the 'tree' in the script list.
 *)
procedure SL_ModifyTree(tree: integer; expand: boolean);
var
  c, i, j: integer;
begin
  for i := 0 to high(SL_Trees) do
  begin
    if (length(SL_Trees[i]) <= 0) then
      continue;

    if (tree = SL_TreeIndexes[i]) then
      with Lists[LIST_ALL_SCRIPTS] do
      begin
        if (expand) then
          Items.Strings[SL_TreeIndexes[i] - 1] := '-   ' + skillToStr(i)
        else
          Items.Strings[SL_TreeIndexes[i] - 1] := '+  ' + skillToStr(i);

        c := 0;

        for j := high(SL_Trees[i]) downto 0 do
        begin
          if (expand) then
            Items.Insert(tree, '         ' + capitalize(MSI_Scripts[SL_Trees[i][j]].Name))
          else
            Items.Delete(tree);

          inc(c);
        end;

        continue;
      end;

    if (expand) then
      incEx(SL_TreeIndexes[i], c)
    else
      decEx(SL_TreeIndexes[i], c);
  end;
end;

(**
 * Expands or contracts the clicked tree in the script list.
 *)
procedure SL_ExpandTree(Sender: TObject);
var
  e: integer;
begin
  e := Lists[LIST_ALL_SCRIPTS].ItemIndex;
  if (e = -1) then
    exit;

  if (pos('+', Lists[LIST_ALL_SCRIPTS].Items.Strings[e]) > 0) then
    SL_ModifyTree(e + 1, true)
  else
    if (pos('-', Lists[LIST_ALL_SCRIPTS].Items.Strings[e]) > 0) then
      SL_ModifyTree(e + 1, false);

  Buttons[BUTTON_SCRIPT_LIST_ADD].Enabled := true;
end;

(**
 * Adds the selected script to the player's script list.
 *)
procedure SL_AddScript(Sender: TObject);
var
  e: integer;
begin
  e := Lists[LIST_ALL_SCRIPTS].ItemIndex;
  if (e = -1) then
    exit;

  // if one of the expandable options (i.e. skill titles)
  if (pos('+', Lists[LIST_ALL_SCRIPTS].Items.Strings[e]) = 1) or
     (pos('-', Lists[LIST_ALL_SCRIPTS].Items.Strings[e]) = 1) then
  begin
    showMessage('Invalid selection');
    exit;
  end;

  // add the splected script to the player's script list
  if (not inList(Lists[LIST_PLAYER_SCRIPTS], removePreSpaces(Lists[LIST_ALL_SCRIPTS].Items.Strings[e]))) then
    Lists[LIST_PLAYER_SCRIPTS].Items.Add(
      removePreSpaces(Lists[LIST_ALL_SCRIPTS].Items.Strings[e])
    );

  Forms[FORM_SCRIPT_LIST].Visible := false;
end;
